Sub AddPicture(ByVal strPic As String, ByVal intLocX As Integer, ByVal intLocY As Integer, _
               ByVal intWidth As Integer, Optional ByVal intHeight As Integer)
    'Usage:    call AddPicture(strPic, intLocX, intLocY, intWidth, intHeight)
    '                         ex1:  AddPicture("myPic.jpg", 50, 100, 700)
    '                         ex2:  AddPicture("myPic.jpg", 50, 100, 700, 300)
    '                         ex3:  AddPicture("C:\myFolder\myPic.jpg", 50, 100, 700, 300)
    'strPic should include the path to the picture.  This can be modified if you have
    'global variables with the path.

    '*** Use ex1 unless you need to specify a different path or you want to set your own aspect ratio. ***
    
    Dim shpPicture As Shape
    Dim dblAspectRatio As Double
    Dim strPath As String
    
    strPath = strPic          'If you want to update the path

    'Check if file exists and display a visual
    If Not FileOrDirExists(strPath) Then
        Call ActiveSheet.Shapes.AddTextEffect(msoTextEffect2, "Image Not Found!" & vbCr, "Arial Black", 36#, False, False, intLocX, intLocY)
        Call ActiveSheet.Shapes.AddTextEffect(msoTextEffect14, strPath, "Garamond", 16#, False, False, intLocX, intLocY + 30).Select
        Exit Sub
    End If
    
    'Exit Sub
    Set shpPicture = ActiveSheet.Shapes.AddPicture(strPath, False, True, intLocX, intLocY, 1, 1)

    shpPicture.ScaleHeight 1, msoTrue
    shpPicture.ScaleWidth 1, msoTrue
    dblAspectRatio = shpPicture.Width / shpPicture.Height

    If intHeight > 0 Then                               'This lets you set both the width and height of the picture if a height number is used
        shpPicture.Width = intWidth
        shpPicture.Height = intHeight
    Else
        shpPicture.Width = intWidth                     'You set the width & this sets the height of the picture for you based on the aspect ratio.
        shpPicture.Height = intWidth / dblAspectRatio
    End If
    
'   Here's another way to do it...
'
'    If blnKeepRatio = True Then
'        shpPicture.Width = intWidth                     'You set the width & this sets the height of the picture for you based on the aspect ratio.
'        shpPicture.Height = intWidth / dblAspectRatio
'    Else
'        shpPicture.Width = intHeight * dblAspectRatio   'You set the height & this sets the width of the picture for you based on the aspect ratio.
'        shpPicture.Height = intHeight
'    End If

End Sub
